plugins {
    id "com.peterabeles.gversion" version "1.10.2"
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id "io.freefair.lombok" version "8.6"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName.set(project.archives_base_name)
}

repositories {
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "com.github.FlorianMichael:DietrichEvents2:v${project.dietrichevents2_version}"
    include "com.github.FlorianMichael:DietrichEvents2:v${project.dietrichevents2_version}"

}

processResources {
    inputs.property("version", project.version)
    inputs.property("minecraft_version", project.minecraft_version)
    inputs.property("loader_version", project.loader_version)
    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
                "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
        )
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

gversion {
    srcDir       = "src/main/java/"
    classPackage = "dev.l4j.currymod"
    className    = "GitInfo"
    dateFormat   = "yyyy-MM-dd'T'HH:mm:ss'Z'"
    timeZone     = "EST"
    debug        = false
    language     = "java"
    explicitType = false
    indent       = "\t"
    annotate     = false
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}

project.compileJava.dependsOn(createVersionFile)
