plugins {
    id "com.peterabeles.gversion" version "1.10.2"
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id "io.freefair.lombok" version "8.6"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroup 'org.ladysnake'
            includeGroupByRegex 'dev\\.onyxstudios.*'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "de.florianmichael:DietrichEvents2:${project.dietrichevents2_version}"
    include "de.florianmichael:DietrichEvents2:${project.dietrichevents2_version}"

    modImplementation "org.ladysnake:satin:${satin_version}"
    include "org.ladysnake:satin:${satin_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

gversion {
    srcDir       = "src/main/java/"
    classPackage = "dev.logging4j.currymod"
    className    = "GitInfo"
    dateFormat   = "yyyy-MM-dd'T'HH:mm:ss'Z'"
    timeZone     = "EST"
    debug        = false
    language     = "java"
    explicitType = false
    indent       = "\t"
    annotate     = false
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.named("jar") {
    from("LICENSE") {
        rename { "${it}_${archiveBaseName.get()}" }
    }
}


publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {}
}

project.compileJava.dependsOn(createVersionFile)